#!/bin/csh -f

if (!(-w $1)) then

  # ---
  # Le fichier n'est pas en checkout, on peut y aller...
  # ---

  # Autoriser l'écriture dans le répertoire
  chmod u+w `dirname $1`

  # Calculer le numéro de la dernière modification
  set n=1
  set nomFichierDiff = `dirname $1`/diff`basename $1`
  while ( -e $nomFichierDiff-$n)
    @ n++
  end
  @ n--
  # n vaut 0 si pas de modifications, ou vaut le nombre de modifications

  if ($n > 0 ) then

    # Suppression du dernier fichier diff
    \rm -f $nomFichierDiff-$n

    # Reconstituer le fichier
    \rm -f $1
    cp $1.orig $1
    set i=1
    while ($i < $n)
      GENERATE $1 $nomFichierDiff-$i > $1.tmp
      mv $1.tmp $1
      @ i++
    end

    # Nettoyage si on détruit la dernière modification
    if ($n == 1) then
      \rm -f $1.orig $nomFichierDiff-1
    endif
  endif

  # Le fichier passe en read-only
  chmod u-w $1

  # Ne plus autoriser l'écriture dans le répertoire
  chmod u-w `dirname $1`

else

  # ---
  # Le fichier est n'est pas en check-in, message d'erreur
  # ---

  echo "Le fichier "$1" n'est pas en check-in"
endif
